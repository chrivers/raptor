FILE = {
    SOI ~ STATEMENT* ~ EOI
}

STATEMENT = {
    NEWLINE
  | (FROM    ~ NEWLINE)
  | (RENDER  ~ NEWLINE)
  | (WRITE   ~ NEWLINE)
  | (COPY    ~ NEWLINE)
  | (INCLUDE ~ NEWLINE)
  | (INVOKE  ~ NEWLINE)
  | (RUN     ~ NEWLINE)
  | (ENV     ~ NEWLINE)
  | (WORKDIR ~ NEWLINE)
}

WHITESPACE = _{ " " | "\\\n" }

COMMENT = _{
    "#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE
}

quoted_string = _{ "\"" ~ string_inner ~ "\"" }

string_inner = {
    (string_non_escape | string_escape_seq)*
}

string_non_escape = {
    (!"\\" ~ ANY)+
}

string_escape_seq = ${ "\\" ~ char_escape }

char_escape = _{ "\\" | "\"" | "n" | "r" | "t" }

literal_string = ${
    (
        '0'..'9'
      | 'a'..'z'
      | 'A' .. 'Z'
      | '!' .. '['
      | ']' .. '~'
    )+
}

literal_path = _ { literal_string }

string = {quoted_string | literal_path}

filename = {
    !("--") ~ (quoted_string | literal_path)
}

mb_eq = _{ "="? }

IDENT_START = _{ ASCII_ALPHA | "_" }
IDENT_CHAR  = _{ ASCII_ALPHA | "_" | ASCII_DIGIT }
ident = { IDENT_START ~ IDENT_CHAR* }

user_spec  = { ident }
group_spec = { ident }

option_chown = ${
    user_spec ~ ":" ~ group_spec
  | user_spec ~ ":"
  | ":" ~ group_spec
  | user_spec
}
option_chmod = ${ ASCII_OCT_DIGIT{3,4} }

file_option = {
    ("--chown" ~ mb_eq ~ option_chown)
  | ("--chmod" ~ mb_eq ~ option_chmod)
}

file_options = {
    file_option*
}

identpath = _{
    ident ~ ("." ~ ident)*
}

bool = {
    "true" | "false"
}

number = {
    ('1'..'9') ~ ('0'..'9')*
}

value = {
    bool | number | quoted_string | identpath
}

include_arg = ${
    ident ~ "=" ~ value
}

include_args = {
    include_arg*
}

env_assign = ${
    ident ~ "=" ~ string
}

COPY = {
    "COPY"
    ~ file_options
    ~ filename{2,}
}

RENDER = {
    "RENDER"
    ~ file_options
    ~ filename{2}
}

WRITE = {
    "WRITE"
    ~ file_options
    ~ filename
    ~ quoted_string
}

INCLUDE = {
    "INCLUDE"
    ~ filename
    ~ include_args
}

INVOKE = {
    "INVOKE"
    ~ quoted_string+
}

RUN = {
    "RUN"
    ~ filename+
}

FROM = {
    "FROM"
    ~ ident
}

ENV = {
    "ENV"
    ~ env_assign+
}

WORKDIR = {
    "WORKDIR"
    ~ filename
}
